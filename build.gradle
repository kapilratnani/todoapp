plugins {
    id "org.sonarqube" version "2.7"
    id 'com.github.sherter.google-java-format' version '0.8'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
}

repositories {
    jcenter()
}

tasks.getByName('sonarqube') {
    doFirst {
        // lazy initialize the property to collect all coverage files
        def jacocoFilesFromSubprojects = subprojects.findAll {it.plugins.hasPlugin('jacoco')}
                .collect {it.tasks.withType(Test)}.flatten().collect {it.jacoco.destinationFile}

        sonarqube.properties {
            property "sonar.jacoco.reportPaths", jacocoFilesFromSubprojects.join(',')
        }
    }
}

subprojects {
    group = 'net.ripper.todoapp'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = 1.8

    apply plugin: 'java'
    apply plugin: "jacoco"
    apply plugin: 'io.spring.dependency-management'
    apply from: rootProject.file('gradle/install-git-hooks.gradle')

    jacoco {
        toolVersion = "0.8.5"
    }

    repositories {
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.1.9.RELEASE")
        }
    }

}